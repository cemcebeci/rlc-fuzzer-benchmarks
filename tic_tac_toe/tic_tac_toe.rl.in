# This file contains an implementation of a __BOARD_SIZE__x__BOARD_SIZE__ tic tac toe. The game crashes when a player wins by
#	filling the second diagonal. We are expecting the fuzzer to find this error.
import serialization.to_byte_vector
import fuzzer.cpp_functions
import fuzzer.utils

fun crash() {false}:
	return

ent Board:
	Int[__BOARD_SIZE_SQUARED__] slots
	Bool playerTurn


	fun get(Int x, Int y) -> Int:
		return self.slots[x + (y * __BOARD_SIZE__)]

	fun set(Int x, Int y, Int val): 
		self.slots[x + (y * __BOARD_SIZE__)] = val

	fun full() -> Bool:
		let x = 0

		while x < __BOARD_SIZE__:
			let y = 0
			while y < __BOARD_SIZE__:
				if self.get(x, y) == 0:
					return false
				y = y + 1
			x = x + 1

		return true

	fun complete_line_player_row(Int player_id, Int row) -> Bool:
		let i = 0
		while i < __BOARD_SIZE__:
			if self.get(i, row) != player_id:
				return false
			i = i + 1
		return true

	fun complete_line_player_col(Int player_id, Int col) -> Bool:
		let i = 0
		while i < __BOARD_SIZE__:
			if self.get(col, i) != player_id:
				return false
			i = i + 1
		return true
	
	fun diagonal_player(Int player_id) -> Bool:
		let i = 0
		while i < __BOARD_SIZE__:
			if self.get(i, i) != player_id:
				return false
			i = i + 1
		return true

	fun inverse_diagonal_player(Int player_id) -> Bool:
		let i = 0
		while i < __BOARD_SIZE__:
			if self.get(i, (__BOARD_SIZE__ - 1) - i) != player_id:
				return false
			i = i + 1
		return true

	fun complete_line_player(Int player_id) -> Bool:
		let x = 0
		while x < __BOARD_SIZE__:
			if self.complete_line_player_col(player_id, x):
				return true

			if self.complete_line_player_row(player_id, x):
				return true
			x = x + 1

		if self.diagonal_player(player_id):
			return true

		if self.inverse_diagonal_player(player_id):
			return true

		return false

	fun current_player() -> Int:
		return int(self.playerTurn) + 1

	fun next_turn():
		self.playerTurn = !self.playerTurn

fun guarded_comparison_condition(Board b, Int x, Int y) -> Bool:
	# little hack here to mimic short-circuit semantics
	let index = (x + (y * __BOARD_SIZE__))
	if index >= __BOARD_SIZE_SQUARED__ or index < 0:
		return false
	return (b.get(x,y) == 0)

act play() -> TicTacToe:
	frm board : Board
	frm score = 10
	while !board.full():
		act mark(Int x, Int y) {
			x < __BOARD_SIZE__,
			x >= 0,
			y < __BOARD_SIZE__,
			y >= 0,
			guarded_comparison_condition(board, x, y)
		}
		score = score - 1

		board.set(x, y, board.current_player())

		if board.complete_line_player(board.current_player()):
			crash()
			return

		board.next_turn()

fun gen_printer_parser():
	let state = play()
	let serialized = as_byte_vector(state)
	from_byte_vector(state, serialized)
